buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

def getVersion = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

group = 'org.slamon'
version = getVersion()
sourceCompatibility = 1.7
targetCompatibility = 1.7

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    buildTypes {
        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 22
            versionCode 1
            versionName version
        }

        debug {
            debuggable true
        }
    }

    sourceSets {
        test {
            java.srcDir file('src/androidTest/java')
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.slamon:slamon-java-lib:1.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'org.jboss.aerogear:aerogear-android-core:2.0.0'
    compile 'org.jboss.aerogear:aerogear-android-pipe:2.0.0'
    compile 'org.jboss.aerogear:aerogear-android-push:2.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'com.google.guava:guava:18.0'
    testCompile 'org.robolectric:robolectric:2.4'
    testCompile 'org.mockito:mockito-core:1.10.18'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'slamon-agent'
        userOrg = System.getenv('BINTRAY_ORG')
        desc = 'SLAMon Android Agent'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/SLAMon/slamon-android'
        issueTrackerUrl = 'https://github.com/SLAMon/slamon-android/issues'
        vcsUrl = 'https://github.com/SLAMon/slamon-android.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SLAMon Android Agent'
                url 'https://github.com/SLAMon/slamon-android'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection 'https://github.com/SLAMon/slamon-android.git'
                    developerConnection 'https://github.com/SLAMon/slamon-android.git'
                    url 'https://github.com/SLAMon/slamon-android'
                }
            }
        }
    }
}
